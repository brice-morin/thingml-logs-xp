import "pim/BreakoutGame.thingml"

thing BreakoutGameNodeJS includes BreakoutGame {	
	function quit() @monitor "not" do
		`if (global.gc) {
			global.gc();
		}`
		println ">done in ", (`Number(` & stopTime & `)` - `Number(` & startTime & `)`), "ms!"
		println ">lives:", lives, ",score:", score, ",level:", level, ",bx:", bx, ",by:", by, ",padx:", padx
		println ">usedMem:", usedMemory()
		`process.exitCode = 1;`
	end
	
	function timestamp() : UInt32 @monitor "not" return `process.hrtime.bigint()/BigInt(1000000)` as UInt32
	
	function usedMemory() : UInt32 @monitor "not" do
		`if (global.gc) {
			global.gc();
		}`
		return `process.memoryUsage().heapUsed` as UInt32
	end
	
	function unsetBit(variable : UInt8, bit : UInt8) : UInt8 return (``&variable&` & ~(1 << `& bit &`)`) as UInt8
}

configuration test
@docker "perf"
{
	instance main : BreakoutGameNodeJS
	instance disp : HeadlessDisplay
	instance timer : TimerNodeJS
	instance ctrl : BasicIAController

	connector main.clock => timer.timer
	connector main.display => disp.display
	connector main.controller => ctrl.controls
	connector ctrl.game => main.ia
}
